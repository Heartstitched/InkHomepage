//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]\f[bold,true]Ok,\w4 what should I call you?\f[bold,false]"
	--
	"\n\n\f[bold,true]\![*]\q[Cancel,OnConfigMenu,OnNameTeach]\*\![open,inputbox,OnNameTeach,-1]\f[bold,false]\e"
}

OnNameTeach
{	
	_tempname = CUTSPACE(reference.raw[0]) //this stores the original name in _tempname
	_name = reference.raw[0] //putting the raw name into _name to do some tests on it
	_name = TOLOWER(_name) //changes it to lowercase
	_name = REPLACE(_name," ","") //removes spaces
	_name = REPLACE(_name,".","") //removes periods
	_name = REPLACE(_name,"-","") //removes dashes
	
	_is_first_boot = 0; if reference2 == "firstboot"; _is_first_boot = 1
	
	if _is_first_boot; "\_l[-10000,-10000]\q[ ,__VERY_FIRST_BOOT__]\_l[0,0]\*\t" //Prevents balloon from timing out if this is during first boot. Also prevents the user from closing out the input box
	--
	if _tempname == username && !_is_first_boot //If the user gives the exact same name as before. The firstboot check has to be there in case they put in 'user'
	{
		"\0\s[0]\f[bold,true]Oh,\w4 did you change your mind?\f[bold,false]"
	}
	elseif _name == "Ink"
	{
		"\0\s[0]\f[bold,true]That's my name!\w8\w8 I can't call you that!\w8\w8 Try again.\f[bold,false]"
		--
		EnterNameAgain
	}
	elseif _name == "" //If the user doesn't put in anything
	{
		username = "User"
		
		"\0\s[0]\f[bold,true]Oh,\w4 ok,\w4 I'll just call you User,\w4 then.\w8\w8 Let me know if you want to change it!\f[bold,false]"
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]\f[bold,true]Ok,\w4 I'll call you %(username) then.\f[bold,false]"
	}
	--
	if !_is_first_boot //If the user was changing their name through the config menu
	{
		"\n\f[bold,true]\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\f[bold,false]\e"
	}
	else //This is continuing from FirstBoot
	{
		"\w8\w8\f[bold,true] Good to meet you!\w8\w8 And how should I refer to you?\f[bold,false]"
		--
		all:/
		{
			"\n\n\t\*"
			"\f[bold,true]\![*]\q[He/Him/His,OnPronouns,masc,firstboot]\f[bold,false]\n"
			"\f[bold,true]\![*]\q[She/Her/Hers,OnPronouns,fem,firstboot]\f[bold,false]\n"
			"\f[bold,true]\![*]\q[They/Them/Their,OnPronouns,undef,firstboot]\f[bold,false]\n"
			"\f[bold,true]\![*]\q[Custom,OnPronouns,custom]\f[bold,false]\n\n"
		}
	}
}

//If the user put in an invalid name
EnterNameAgain
{
	_is_first_boot = 0; if reference2 == "firstboot"; _is_first_boot = 1
	_function = "OnNameTeach"; if _is_first_boot; _function = "OnFirstNameTeach"
	
	if !_is_first_boot; "\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]" //If changing name through the menu
	--
	"\![open,inputbox,OnNameTeach,-1,--reference=firstboot]\e"
}


OnChangePronoun //Envelope names are the same as the GT template, since I figured most people will be used to them.
{
	"\0\s[0]\f[bold,true]How should I refer to you?\f[bold,false]\w5"
	--
	all:{
	"\n\n\_q"
	"\f[bold,true]\![*]\q[He/Him/His,OnPronouns,hehimhis]\f[bold,false]\n"
	"\f[bold,true]\![*]\q[She/Her/Hers,OnPronouns,sheherhers]\f[bold,false]\n"
	"\f[bold,true]\![*]\q[They/Them/Their,OnPronouns,theythemtheirs]\f[bold,false]\n"
	"\f[bold,true]\![*]\q[Custom,OnPronouns,custom]\f[bold,false]\n\n"
	
	"\f[bold,true]\![*]\q[Cancel,OnConfigMenu]\f[bold,false]\e"
	}
}

//custom pronouns code adapted from SmokyCinnamonRoll#7652 on the ukgk discord 
OnPronouns
{

    if reference0 == "custom" {

		"\![raise,OnSubjectPronounTeach,%(reference1)]\e"

    }  	
	else { //one of the boilerplate pronouns picked
	
		if reference0 == "hehimhis" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "singular"
			pronounsArray[1] = "he"
			pronounsArray[2] = "him"
			pronounsArray[3] = "his"
			pronounsArray[4] = "his"
			pronounsArray[5] = "himself"
		
		}
		elseif reference0 == "sheherhers" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "singular"
			pronounsArray[1] = "she"
			pronounsArray[2] = "her"
			pronounsArray[3] = "her"
			pronounsArray[4] = "hers"
			pronounsArray[5] = "herself"
		
		} 
		elseif reference0 == "theythemtheirs" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "plural"
			pronounsArray[1] = "they"
			pronounsArray[2] = "them"
			pronounsArray[3] = "their"
			pronounsArray[4] = "theirs"
			pronounsArray[5] = "themself"
		
		} else { //reference0 is singular or plural
			pronounsArray[0] = reference0
		}
		

	}
	--
	if reference1 == "firstboot" //This is the end of the firstboot dialogue
	{
		"\f[bold,true]\0\s[0]Alright.\w8 I will use %(subjectP), %(objectP), %(possessiveD), %(possessiveP), and %(reflexiveP) for you.\w8 You can double click me to open my menu, and access additional options under config!\f[bold,false]\e"
	}
	--
	"\f[bold,true]\0\s[0]Ok,\w4 I'll use %(subjectP), %(objectP), %(possessiveD), %(possessiveP), and %(reflexiveP) for you.\f[bold,false]"
	--
	"\f[bold,true]\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\f[bold,f]\e"
	
}

OnSubjectPronounTeach{

	LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Please enter the subject pronoun such as:\n\n\f[bold,false]They\f[bold,true] have a dog.\f[bold,false] \![open,inputbox,OnObjectPronounTeach,-1,--reference=%(reference0)]\_q\e"

}

OnObjectPronounTeach{

	LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	
	if reference0 == "" {
	
		"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the subject pronoun such as:\n\n\f[bold,false]They\f[bold,true] have a dog.\f[bold,true]"
		--
		"\![open,inputbox,OnObjectPronounTeach,-1,--reference=%(reference2)]\_q\e"
	
	}
	--
	pronounsArray[1] = reference0

	"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Please enter the object pronoun such as:\n\nI gave \f[bold,false]them\f[bold,true] a dog.\f[bold,false] \![open,inputbox,OnPossessiveDeterminerTeach,-1,--reference=%(reference2)]\_q\e"

}

OnPossessiveDeterminerTeach{


	if reference0 == "" {
	
		"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the object pronoun such as:\n\nI gave \f[bold,false]them\f[bold,true] a dog.\f[bold,false]"
		--
		"\![open,inputbox,OnPossessiveDeterminerTeach,-1,--reference=%(reference2)]\_q\e"
	
	}
	--
	pronounsArray[2] = reference0

	"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Please enter the posessive determiner such as:\n\nThat is \f[bold,false]their\f[bold,true] dog.\f[bold,false] \![open,inputbox,OnPossessivePronounTeach,-1,--reference=%(reference2)]\_q\e"

}

OnPossessivePronounTeach{

	if reference0 == "" {
	
		"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the posessive determiner such as:\n\nThat is \f[bold,false]their\f[bold,true] dog.\f[bold,false] "
		--
		"\![open,inputbox,OnPossessivePronounTeach,-1,--reference=%(reference2)]\_q\e"
	
	}
	--
	pronounsArray[3] = reference0

	"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Please enter the posessive pronoun such as:\n\nThe dog is \f[bold,false]theirs. \![open,inputbox,OnReflexivePronounTeach,-1,--reference=%(reference2)]\_q\e"

}

OnReflexivePronounTeach{

	if reference0 == "" {
	
		"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the posessive pronoun such as:\n\nThe dog is \f[bold,false]theirs. "
		--
		"\![open,inputbox,OnReflexivePronounTeach,-1,--reference=%(reference2)]\_q\e"
	
	}
	--
	pronounsArray[4] = reference0

	"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Please enter the reflexive pronoun such as:\n\nThey bought the dog \f[bold,false]themself. \![open,inputbox,OnSingularPluralTeach,-1,--reference=%(reference2)]\_q\e"

}

OnSingularPluralTeach{

	LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	
	if reference0 == "" {
	
		"\0\s[0]\f[bold,true]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the reflexive pronoun such as:\n\nThey bought the dog \f[bold,false]themself. "
		--
		"\![open,inputbox,OnSingularPluralTeach,-1,--reference=%(reference2)]\_q\e"
	
	}
	--
	pronounsArray[5] = reference0
	
	all:{	
		"\f[bold,true]\0\s[0]\![set,balloontimeout,-1]Is this a singular or plural pronoun?\n\nSingular: She \f[bold,false]is\f[bold,true] kind.\nPlural: They \f[bold,false]are\f[bold,true] kind.\f[bold,false]\n\n"	
		"\f[bold,true]\![*]\q[Singular,OnPronouns,singular,%(reference2)]\f[bold,false]\n"
		"\f[bold,true]\![*]\q[Plural,OnPronouns,plural,%(reference2)]\n\f[bold,false]\e"
	}

}


//Pronoun examples for my own sake:
//subject: they
//object: them
//possessive determiner: their
//possessive pronoun: theirs 
//reflexive pronoun: themselves


OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	"\0\s[0]\f[bold,true]Sure!\w8\w8 What month were you born in?\w8\w8 Name or number.\f[bold,false]"
	--
	"\n\n\f[bold,true]\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\*\f[bold,false]\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers
OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	_month = 0
	
	if _tempmonth == "january" || _tempmonth == "1" || _tempmonth == "01"; _month = 1
	elseif _tempmonth == "february" || _tempmonth == "2" || _tempmonth == "02"; _month = 2
	elseif _tempmonth == "march" || _tempmonth == "3" || _tempmonth == "03"; _month = 3
	elseif _tempmonth == "april" || _tempmonth == "4" || _tempmonth == "04"; _month = 4
	elseif _tempmonth == "may" || _tempmonth == "5" || _tempmonth == "05"; _month = 5
	elseif _tempmonth == "june" || _tempmonth == "6" || _tempmonth == "06"; _month = 6
	elseif _tempmonth == "july" || _tempmonth == "7" || _tempmonth == "07"; _month = 7
	elseif _tempmonth == "august" || _tempmonth == "8" || _tempmonth == "08"; _month = 8
	elseif _tempmonth == "september" || _tempmonth == "9" || _tempmonth == "09"; _month = 9
	elseif _tempmonth == "october" || _tempmonth == "10"; _month = 10
	elseif _tempmonth == "november" || _tempmonth == "11"; _month = 11
	elseif _tempmonth == "december" || _tempmonth == "12"; _month = 12
	else //if the month isn't a valid month, like 32 or something.
	{
		"\f[bold,true]\0\s[0]%(_tempmonth) isn't a valid month,\w4 try again.\f[bold,false]"
		--
		"\n\n\f[bold,true]\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\f[bold,false]\*\e"
	}
	--
	//If it's a valid month
	"\f[bold,true]%(Capitalize(MonthName(_month))) huh?\w8\w8 Ok,\w4 what day?\f[bold,false]"
	--
	"\n\n\f[bold,true]\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1,--reference=%(_month)]\f[bold,false]\*\e"
}

OnUserBornDay
{
	
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = reference2 //Just for readability
	
	if IsValidDay(_month,_tempday)
	{
		birthday[0] = "%(Capitalize(MonthName(_month))) %(NumSuffix(_tempday))"
		birthday[1] = "%(_month)/%(_tempday)" //If you want to write dates as DD/MM, feel free to swap this around
		
		if birthday[1] == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
		{
			"\0\s\f[bold,true][0]Happy birthday,\w4 %(username)!\f[bold,false]"
		}
		elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
		{
			"\0\s[0]\f[bold,true]Oh,\w4 you were born on Christmas?\w8\w8 Cool!\f[bold,false]"
		}
		else //if the birthday is not listed above.
		{
			"\0\s[0]\f[bold,true]Ok, your birthday is %(birthday[0]).\w8\w8 I'll remember.\f[bold,false]"
		}
		--
		"\n\n\f[bold,true]\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\f[bold,false]\e"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]\f[bold,true]That isn't a valid day.\w8\w8 Try again.\f[bold,false]"
		--
		"\n\n\f[bold,true]\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\f[bold,false]\*\e"
	}
	
}