OnTranslate
{
    _talk = reference0
    
    //This is what makes %(embedded_elements) work in script input
    if reference1 == "" && reference2 == "" //If this is from the input box
    { //send input box : no event (ref2) , no special flag (ref1)
        _talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
    }
    
    //If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
    _talk = REPLACE(_talk,"DreamChitChat","\_a[OnFriend.Dream]Dream\_a")
    _talk = REPLACE(_talk,"BlueChitChat","\_a[OnFriend.Blue]Blue\_a")
	_talk = REPLACE(_talk,"ErrorChitChat","\_a[OnFriend.Error]Error\_a")
	_talk = REPLACE(_talk,"FreshChitChat","\_a[OnFriend.Fresh]Fresh\_a")
	
	
    TOSTR(_talk)
}

OnFriend.Dream
{
  "\0\s[5]\f[bold,true]Oh Dream...\w8\w4 \n\s[2]You know,\w3 he's got a good motive.\w5 \n\s[20]I can appreciate it,\w8 \s[22]even if it isnt always to the script.... \f[bold,false]\e"
}

OnFriend.Blue
{
  "\0\s[16]\f[bold,true] Aww baby Blue! \w8\s[4]Hes always so excitable....\f[bold,false]\e"
}

OnFriend.Error
{
  "\0\s[24]\f[bold,true]That guy is so\w3o\w2o\w2 crabby sometimes!! \f[bold,false]\e"
}

OnFriend.Fresh
{
  "\0\s[3]\f[bold,true]An interesting one, that guy..... \s[8]I wonder what hes up to now?\f[bold,false]\e"
}


//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
	
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[10]\0\s[0]"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	"\1\s[10]\0\s[0]"
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	"https://raw.githubusercontent.com/Heartstitched/InkHomepage/master/Updates/HS_Ink/"
}



//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}
On_recommendrootbutton.caption
{
	"Look who's here!"
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Comyet, Ink's creator!";	"https://comyet.tumblr.com"
	"The silly lady who put this dude together";	"https://www.tumblr.com/monster-kind"
	"Let Ink know your stuff! He might talk about it :)";	"https://www.tumblr.com/inks-inbox"
}


On_portalrootbutton.caption
{
	"Reference Sites"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
	
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
"Ghost Template Walkthrough";	"http://www.ashido.com/ukagaka/walkthrough.html"
	"SSP";	"http://ssp.shillest.net/"
	"Crow + SSP Reference (J)"; 	"http://crow.aqrs.jp/reference/all/index.html"
	"Ghost Documentation (J)";	"http://usada.sakura.vg/contents/specification.html"
	"Disc-2 (J)";	"http://disc2.s56.xrea.com/"
	"Ukagaka Dream Team Wiki";  "https://ukagakadreamteam.com/wiki/home"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

StuffToReset
{
	KiaMode = 0
	BoneJMode = 0

}
//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%('ve','s') found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/



//Written by Zichqec https://ukagaka.zichqec.com/
/*
This function and event are for quickly checking if a character is wearing a given dressup. Something like "If they're wearing a scarf, do this".
This is intended to be simplified for folks that just need quick, easy checks. If your dressups are more involved or you want to do more complicated checks, you may need to write some custom code in OnNotifyDressupInfo. **This is not the only way to do dressup checks.**

Arguments:
Arguments are written as a **single comma separated string** rather than separate elements.
Arg 1: Which character (0 for sakura, 1 for kero, 2+ for additional characters...)
Arg 1: Dressup category
Arg 2: Dressup name

Usage example:
if WearingDressup("0,Hat,Silly")
{
	"\0\s[0]I'm wearing a silly hat!"
}

Customizing this function:
- If you only have one character, then you may want to omit the need for specifying which character a dressup is on.
- If all of your items have completely unique names, you may want to omit the need to specify which category the item is.
However you want to change it, just go to the _info array created in OnNotifyDressupInfo (there's a comment just above it showing where it's at), and edit what parameters are included/what order they're listed in to suit you.
For example, if you change it to _info = "%(_item_name),%(_category)"
Then you can write your check like if WearingDressup("Silly,Hat")

Note:
This makes use of the OnNotifyDressupInfo event, which you can read more about here: https://ukagakadreamteam.github.io/ukadoc/manual/list_shiori_event.html#OnNotifyDressupInfo
If you already have OnNotifyDressupInfo, you may need to modify it to suit this function.

This also makes use of a global variable called CurrentDressups.
You should initialize CurrentDressups with the following code in OnFirstBoot:
CurrentDressups = IARRAY
*/

WearingDressup
{
	if ASEARCH(_argv[0],CurrentDressups) != -1; 1
	else; 0
}

//[character ID, category name, part name, option, on-1/off-0, thumbnail path]
OnNotifyDressupInfo
{
	CurrentDressups = IARRAY
	foreach reference; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		_id = _dressup[0]
		_category = _dressup[1]
		_item_name = _dressup[2]
		
		//Edit here if you want to change what all you have to specify! This just specifies the form you want the dressups to be listed as.
		_info = "%(_item_name)"
		
		if _dressup[4] == "1"; CurrentDressups ,= _info
	}

	
}
//Copied from the base SHIORI files, but with the username variable added
OnNotifyUserInfo : void
//{
//	SHIORI3FW.UserName = reference[0]
//	username = reference[0]
//	SHIORI3FW.UserNameFull = reference[1]
//	SHIORI3FW.UserBirthday = SPLIT(reference[2],',',3)
//	for _i = 0 ; _i < 3 ; _i++ {
//		SHIORI3FW.UserBirthday[_i] = TOINT(SHIORI3FW.UserBirthday[_i])
//	}
//	SHIORI3FW.UserSex = reference[3]
//}

